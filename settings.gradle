import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        mavenLocal()

        maven {
            name("Fabric Maven")
            url("https://maven.fabricmc.net")
        }

        maven {
            name("Jitpack Maven")
            url("https://jitpack.io")
        }

        maven {
            name("Nyan Maven")
            url("https://maven.hendrixshen.top")
        }

        maven {
            name("Cotton")
            url("https://server.bbkr.space/artifactory/libs-release")
        }

        mavenCentral()
        gradlePluginPortal()
    }

    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": {
                    useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
                    break
                }
            }
        }
    }
}

Map<String, List<String>> settings = file("settings.json").withReader {
    new JsonSlurper().parse(it) as Map<String, List<String>>
}

for (String version : settings.versions) {
    include(":$version")
    ProjectDescriptor proj = project(":$version")
    proj.setProjectDir(file("versions/$version"))
    proj.setBuildFileName("../${settings.versions.get(0)}/build.gradle")
}

include(":fabricWrapper")
